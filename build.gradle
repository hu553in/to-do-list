plugins {
    id 'java'
    id 'jacoco'

    id 'io.freefair.lombok' version "$lombokPluginVersion"
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementPluginVersion"
    id 'org.springdoc.openapi-gradle-plugin' version "$springdocPluginVersion"
    id 'com.github.spotbugs' version "$spotBugsVersion"
}

group = 'com.github.hu553in'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of("$javaVersion")
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: "$mapStructVersion"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    compileOnly group: 'io.jsonwebtoken', name: 'jjwt-api', version: "$jjwtVersion"
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: "$jjwtVersion"
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: "$jjwtVersion"
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-core'
    implementation group: 'org.mapstruct', name: 'mapstruct', version: "$mapStructVersion"
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: "$springdocVersion"
    testImplementation group: 'org.testcontainers', name: 'postgresql', version: "$testcontainersVersion"
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: "$testcontainersVersion"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

spotbugsMain {
    reports {
        xml.required = true
        html.required = true
    }
}

spotbugsTest {
    reports {
        xml.required = true
        html.required = true
    }
}

check {
    dependsOn {
        spotbugsMain
        spotbugsTest
    }
}

test {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

jar {
    enabled = false
}
